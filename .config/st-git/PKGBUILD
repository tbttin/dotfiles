# Maintainer: Tin Thai <thaibachthanhtin@gmail.com>

_pkgname='st'
pkgname="${_pkgname}-git"
pkgver=0.9.r11.g9846a56
pkgrel=1
pkgdesc='A simple virtual terminal emulator for X'
arch=('i686' 'x86_64')
url='https://st.suckless.org'
license=('custom:MIT')
depends=('libx11' 'libxft' 'fontconfig' 'freetype2')
makedepends=('git')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
_patches=(
  'st-fix-terminfo-20231224.patch'
  'https://st.suckless.org/patches/bold-is-not-bright/st-bold-is-not-bright-20190127-3be4cf1.diff'
)
sha256sums=(
  'SKIP'
  'SKIP'
  '2c231312204ce2455bcb29417215775b8ff5ec4c7b6eec229dd3fbcf87581053'
  '329169acac7ceaf901995d6e0897913089b799d8cd150c7f04c902f4a5b8eab2'
)
source=(
  "git+https://git.suckless.org/${_pkgname}"
  'config.h'
  ${_patches[@]}
)

pkgver() {
  cd "${_pkgname}"
  /usr/bin/git describe --long --tags |
    /usr/bin/sed --regexp-extended 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  cd "${_pkgname}"
  if [ -f "${srcdir}/config.h" ]
  then
    /usr/bin/cp --force --verbose "${srcdir}/config.h" .
  fi
  local patch_file
  for patch_file in ${_patches[@]##*/}
  do
    /usr/bin/echo "Applying ${patch_file}..."
    /usr/bin/patch --silent --input="${srcdir}/${patch_file}"
  done
}

build() {
  cd "${_pkgname}"
  /usr/bin/make\
    X11INC=/usr/include/X11\
    X11LIB=/usr/lib/X11
}

package() {
  cd "${_pkgname}"
  /usr/bin/make DESTDIR="${pkgdir}" PREFIX='/usr' install
  local ins_opts='-D --mode=644'
  /usr/bin/install ${ins_opts}\
    LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  /usr/bin/install ${ins_opts}\
    README  "${pkgdir}/usr/share/doc/${pkgname}/README"
}

