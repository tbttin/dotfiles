# Maintainer: Tin Thai <thaibachthanhtin@gmail.com>

_pkgname='st'
pkgname="${_pkgname}-git"
pkgver=0.8.4.r7.g2f6e597
pkgrel=1
pkgdesc='A simple virtual terminal emulator for X.'
arch=('i686' 'x86_64' 'armv7h')
url='https://st.suckless.org'
license=('custom:MIT')
depends=('libxft')
makedepends=('ncurses' 'libxext' 'git')
provides=(${_pkgname})
conflicts=(${_pkgname})
_patches=(
    'https://st.suckless.org/patches/bold-is-not-bright/st-bold-is-not-bright-20190127-3be4cf1.diff'
)
source=(
    "git+https://git.suckless.org/${_pkgname}"
    'config.h'
    ${_patches[@]}
)
sha256sums=(
    'SKIP'
    'SKIP'
    '329169acac7ceaf901995d6e0897913089b799d8cd150c7f04c902f4a5b8eab2'
)

pkgver() {
    cd ${_pkgname}
    git describe --long --tags | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
    cd ${_pkgname}
    if [ -f "${srcdir}/config.h" ]; then
        echo "Copying config.h ..."
        cp -f "${srcdir}/config.h" .
    fi
    for patch in ${_patches[@]}; do
        pfile=${patch##*/}
        echo "Applying ${pfile} ..."
        git apply "${srcdir}/${pfile}"
    done
}

build() {
    cd ${_pkgname}
    make \
        X11INC=/usr/include/X11 \
        X11LIB=/usr/lib/X11
}

package() {
    cd ${_pkgname}
    make PREFIX=/usr DESTDIR="${pkgdir}" install
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
