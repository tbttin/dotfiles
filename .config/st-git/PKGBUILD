# Maintainer: Tin Thai <thaibachthanhtin@gmail.com>

_pkgname=st
pkgname=$_pkgname-git
pkgver=0.8.4.r2.g4ef0cbd
pkgrel=1
pkgdesc='A simple virtual terminal emulator for X.'
arch=('i686' 'x86_64' 'armv7h')
url=https://st.suckless.org
license=('MIT')
depends=('libxft')
makedepends=('ncurses' 'libxext' 'git')
provides=($_pkgname)
conflicts=($_pkgname)
source=('git+https://git.suckless.org/st'
        'config.h'
        'https://st.suckless.org/patches/bold-is-not-bright/st-bold-is-not-bright-20190127-3be4cf1.diff'
        'https://st.suckless.org/patches/anysize/st-anysize-0.8.1.diff')
sha256sums=('SKIP'
            'SKIP'
            '329169acac7ceaf901995d6e0897913089b799d8cd150c7f04c902f4a5b8eab2'
            '8118dbc50d2fe07ae10958c65366476d5992684a87a431f7ee772e27d5dee50f')

pkgver() {
    cd $_pkgname
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd $_pkgname
    if [ -f "$srcdir/config.h" ]; then
        cp -fv "$srcdir/config.h" .
    fi
    for patch in $srcdir/*.diff; do
        echo "Applying $patch ..."
        patch -s -i "$patch"
    done;
}

build() {
    cd $_pkgname
    make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
    cd $_pkgname
    make PREFIX=/usr DESTDIR="$pkgdir" install
    install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
