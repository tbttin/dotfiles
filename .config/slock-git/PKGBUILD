# Maintainer: Tin Thai <thaibachthanhtin@gmail.com>

_pkgname=slock
pkgname=${_pkgname}-git
pkgver=1.4.r5.g35633d4
pkgrel=1
pkgdesc='Simple X display locker.'
arch=('i686' 'x86_64' 'armv7h')
url='https://tools.suckless.org/slock'
license=('custom:MIT')
depends=('libxrandr')
makedepends=('git')
provides=($_pkgname)
conflicts=($_pkgname)
_patches=(
    # Missleading indentation warning.
    'https://tools.suckless.org/slock/patches/blur-pixelated-screen/slock-blur_pixelated_screen-1.4.diff'
)
source=(
    'git+https://git.suckless.org/slock'
    'config.h'
    ${_patches[@]}
)
sha256sums=(
    'SKIP'
    'SKIP'
    '07254d038a731ee16781f13ba5ba77da9200a4e8e026a30a5f3674f632b9e42e'
)

pkgver() {
    cd $_pkgname
    git describe --long --tags | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
    cd $_pkgname
    if [ -f "${srcdir}/config.h" ]; then
        echo "Copying config.h ..."
        cp -f "${srcdir}/config.h" .
    fi
    for patch in ${_patches[@]}; do
        pfile=${patch##*/}
        echo "Applying ${pfile} ..."
        git apply "${srcdir}/${pfile}"
    done
}

build() {
    cd $_pkgname
    make \
        X11INC=/usr/include/X11 \
        X11LIB=/usr/lib/X11
}

package() {
    cd $_pkgname
    make PREFIX=/usr DESTDIR="$pkgdir" install
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
