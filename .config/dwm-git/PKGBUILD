# Maintainer: Tin Thai <thaibachthanhtin@gmail.com>

_pkgname='dwm'
pkgname="${_pkgname}-git"
pkgver=6.2.r0.gcb3f58a
pkgrel=1
pkgdesc='A dynamic window manager for X11'
arch=('i686' 'x86_64')
url='https://dwm.suckless.org'
license=('custom:MIT')
depends=('libx11' 'libxinerama' 'fontconfig' 'libxft') # 'freetype2'
optdepends=(
	'dmenu: dynamic menu'
	'st:    a simple terminal'
)
makedepends=('git')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
_patches=(
	'dwm-fix-ffplay-transient-20201210-6.2.diff'
	'https://dwm.suckless.org/patches/notitle/dwm-notitle-6.2.diff'
	'https://dwm.suckless.org/patches/smartborders/dwm-smartborders-6.2.diff'
	'https://dwm.suckless.org/patches/switchtotag/dwm-switchtotag-6.2.diff'
)
source=(
	"git+https://git.suckless.org/${_pkgname}#tag=6.2"
	'config.h'
	${_patches[@]}
)
sha256sums=(
	'SKIP'
	'SKIP'
	'957f38f2fc53406e36891af7506165e4e5efd4e316d64084275cee8a7e907f60'
	'2ee1347c84428facff61830133146dd5467b0f9642703866e414212651a22753'
	'bab74f6bf558a6a3b0b97c5727baab4ec23992a9e23a289888a1dbd6441c1d32'
	'1e3ff12db59c7ec364f07d31c6383ec63fe2d6857f2298141b433612a7ebc7f8'
)

pkgver() {
	cd "${_pkgname}"
	/usr/bin/git describe --long --tags |
			/usr/bin/sed --regexp-extended 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
	cd "${_pkgname}"
	if [ -f "${srcdir}/config.h" ]
	then
		/usr/bin/cp --force --verbose "${srcdir}/config.h" .
	fi
	local patch_file
	for patch_file in ${_patches[@]##*/}
	do
		/usr/bin/echo "Applying ${patch_file}..."
		/usr/bin/patch --silent --input="${srcdir}/${patch_file}"
	done
}

build() {
	cd "${_pkgname}"
	# Use system flags in /etc/makepkg.conf
	/usr/bin/sed --in-place 's/^\(CFLAGS *\)=/\1+=/g' config.mk
	/usr/bin/sed --in-place 's/^\(LDFLAGS *\)=/\1+=/g' config.mk
	/usr/bin/make \
			X11INC=/usr/include/X11 \
			X11LIB=/usr/lib/X11
}

package() {
	cd "${_pkgname}"
	/usr/bin/make DESTDIR="${pkgdir}" PREFIX='/usr' install
	local ins_opts='-D --mode=644'
	/usr/bin/install ${ins_opts}\
			LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
	/usr/bin/install ${ins_opts}\
			README "${pkgdir}/usr/share/doc/${pkgname}/README"
}

# vim: filetype=sh

