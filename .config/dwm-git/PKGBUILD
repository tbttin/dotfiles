# Maintainer: Tin Thai <thaibachthanhtin@gmail.com>

_pkgname='dwm'
# git rev-parse 6.2
_tag='cb3f58ad06993f7ef3a7d8f61468012e2b786cab'
_srcurl="https://git.suckless.org/${_pkgname}#tag=${_tag}"
pkgname="${_pkgname}-git"
pkgver=6.2.r0.gcb3f58a
pkgrel=1
pkgdesc='A dynamic window manager for X11'
arch=('i686' 'x86_64')
url='https://dwm.suckless.org'
license=('custom:MIT')
depends=(
         'libx11'
         'libxinerama'
         'fontconfig'
         'libxft'
         'freetype2'
         'scrot'
)
optdepends=(
            'st: a simple terminal'
)
makedepends=('git')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
_patches=(
          'dwm-fix-ffplay-transient-20201210-6.2.diff'
          'https://dwm.suckless.org/patches/notitle/dwm-notitle-6.2.diff'
          'https://dwm.suckless.org/patches/noborder/dwm-noborder-6.2.diff'
          'https://dwm.suckless.org/patches/switchtotag/dwm-switchtotag-6.2.diff'
)
source=(
        "git+${_srcurl}"
        'config.h'
        ${_patches[@]}
)
sha512sums=(
            'SKIP'
            'SKIP'
            'f3ab4ced2fa943e6930afa64606de74d74f28680fcb418e5e97c2229849035f8c2d9f18946dd7a5c418b55a6caf0befc49ddedcb38d9c0f9ed90eb1df8318def'
            '3e208fc162ceba77e711066cdfa205733a1c1d186c809e837bf9810b2d18b8b5624828a87f670e6d3ac46876a3abded88c50fdd7afd1a2ef1cc9d478b2459aaa'
            '43a3f39e6f27ff4dfa44264cca195de323b8b83bd734f61001ec23e8644a12078757c26f84165030a0d59594122d9e56e3fa1fc22ecfcaf436e642115bce4927'
            'b2df30caa5a04c1cbcf74b1ac7e6cee10026a002a5280e9d5d9d143ad22fe91d11342966a1dc21d7d742ce48e0df148926861a40d1590c11836f2f436c1ed9b9'
)

prepare() {
  cd "${srcdir}/${_pkgname}"
  # Copy sym linked config.h in src to src/dwm
  if [ -f "${srcdir}/config.h" ]
  then
    /usr/bin/cp --verbose --force "${srcdir}/config.h" config.h
  fi
  # Patch patches.
  local patch_file
  for patch_file in ${_patches[@]##*/}
  do
    /usr/bin/echo "Applying ${patch_file}..."
    /usr/bin/patch --silent --input="${srcdir}/${patch_file}"
  done
  # Do not overwrite system compiler and linker flags in /etc/makepkg.conf
  /usr/bin/sed --in-place 's/^\(\(C\|LD\)FLAGS *\)=/\1+=/g' config.mk
}

pkgver() {
  cd "${srcdir}/${_pkgname}"
  /usr/bin/git describe --long --tags |
    /usr/bin/sed --regexp-extended 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  cd "${srcdir}/${_pkgname}"
  /usr/bin/make \
    X11INC=/usr/include/X11 \
    X11LIB=/usr/lib/X11
}

package() {
  # Installation
  cd "${srcdir}/${_pkgname}"
  # Binaries
  local prefix='/usr'
  /usr/bin/make DESTDIR="${pkgdir}" PREFIX="${prefix}" install
  local ins_opts='-D --mode=644'
  local prefix="${pkgdir}/${prefix}"
  # Licenses
  /usr/bin/install ${ins_opts} \
    LICENSE "${prefix}/share/licenses/${pkgname}/LICENSE"
  # Docs
  /usr/bin/install ${ins_opts} \
    README "${prefix}/share/doc/${pkgname}/README"
}

# vim: ft=PKGBUILD

