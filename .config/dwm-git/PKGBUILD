# Maintainer: Tin Thai <thaibachthanhtin@gmail.com>

_pkgname='dwm'
pkgname="${_pkgname}-git"
pkgver=6.2
pkgrel=1
pkgdesc='A dynamic window manager for X11'
arch=('i686' 'x86_64')
url='https://dwm.suckless.org'
license=('custom:MIT')
depends=('libx11' 'libxinerama' 'fontconfig' 'libxft') # 'freetype2'
optdepends=(
  'dmenu: dynamic menu'
  'st:    a simple terminal'
)
makedepends=('git')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
_patches=(
  'dwm-fix-ffplay-transient-20201210-6.2.diff'
  'https://dwm.suckless.org/patches/notitle/dwm-notitle-6.2.diff'
  'https://dwm.suckless.org/patches/noborder/dwm-noborder-6.2.diff'
  'https://dwm.suckless.org/patches/switchtotag/dwm-switchtotag-6.2.diff'
)
sha256sums=(
  'SKIP'
  'SKIP'
  '957f38f2fc53406e36891af7506165e4e5efd4e316d64084275cee8a7e907f60'
  '2ee1347c84428facff61830133146dd5467b0f9642703866e414212651a22753'
  '9bbf5f963e5a2d23ae4b8731f0c179a8615de5715a2dbf683fbe02115e24efe0'
  '1e3ff12db59c7ec364f07d31c6383ec63fe2d6857f2298141b433612a7ebc7f8'
)
source=(
  "git+https://git.suckless.org/${_pkgname}#tag=$pkgver"
  'config.h'
  ${_patches[@]}
)

# pkgver() {
#   cd "${_pkgname}"
#   /usr/bin/git describe --long --tags |
#       /usr/bin/sed --regexp-extended 's/([^-]*-g)/r\1/;s/-/./g'
# }

prepare() {
  cd "${_pkgname}"
  # Copy sym linked config.h in src/ to src/dwm/
  if [ -f "${srcdir}/config.h" ]
  then
    /usr/bin/cp --verbose --force "${srcdir}/config.h" config.h
  fi
  # Patch patches.
  local patch_file
  for patch_file in ${_patches[@]##*/}
  do
    /usr/bin/echo "Applying ${patch_file}..."
    /usr/bin/patch --silent --input="${srcdir}/${patch_file}"
  done
  # Do not overwrite system compiler and linker flags in /etc/makepkg.conf
  /usr/bin/sed --in-place 's/^\(\(C\|LD\)FLAGS *\)=/\1+=/g' config.mk
}

build() {
  cd "${_pkgname}"
  /usr/bin/make\
    X11INC=/usr/include/X11\
    X11LIB=/usr/lib/X11
}

package() {
  cd "${_pkgname}"
  /usr/bin/make DESTDIR="${pkgdir}" PREFIX='/usr' install
  local ins_opts='-D --mode=644'
  /usr/bin/install ${ins_opts}\
    LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  /usr/bin/install ${ins_opts}\
    README "${pkgdir}/usr/share/doc/${pkgname}/README"
}

