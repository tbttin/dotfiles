# Maintainer: Tin Thai <thaibachthanhtin@gmail.com>

_pkgname='dmenu'
pkgname="${_pkgname}-git"
pkgver=5.0.r4.gd78ff08
pkgrel=1
pkgdesc='A generic menu for X.'
arch=('i686' 'x86_64')
url='https://tools.suckless.org/dmenu/'
license=('custom:MIT')
depends=('sh' 'libxinerama' 'libxft')
makedepends=('git')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("git+https://git.suckless.org/${_pkgname}")
sha256sums=('SKIP')

pkgver() {
    cd ${_pkgname}
    git describe --long --tags | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
    cd ${_pkgname}
    make \
        X11INC=/usr/include/X11 \
        X11LIB=/usr/lib/X11
}

package() {
    cd ${_pkgname}
    make PREFIX=/usr DESTDIR="${pkgdir}" install
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
