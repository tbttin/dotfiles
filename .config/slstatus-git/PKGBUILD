# Maintainer: Tin Thai <thaibachthanhtin@gmail.com>

_pkgname=slstatus
pkgname=$_pkgname-git
pkgver=r552.b14e039
pkgrel=1
pkgdesc='A status monitor for window managers.'
arch=('i686' 'x86_64')
url='https://tools.suckless.org/slstatus'
depends=('libx11')
makedepends=('git')
license=('custom:ISC')
_patches=(
    'slstatus-rmtrailingspacenetspeeds-20201104-b14e039.diff'
)
source=(
    "git+https://git.suckless.org/${_pkgname}"
    'config.h'
    "${_patches[@]}"
)
md5sums=(
    'SKIP'
    'SKIP'
    'fddf6bc672a8be9f77418f7b9caa984b'
)

pkgver() {
    cd $_pkgname
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd $_pkgname
    if [ -f "$srcdir/config.h" ]; then
        echo "Copying config.h ..."
        cp -f "$srcdir/config.h" .
    fi
    for patch in ${_patches[@]}; do
        pfile=${patch##*/}
        echo "Applying ${pfile} ..."
        patch -s -i "$srcdir/${pfile}"
    done
}

build() {
    cd $_pkgname
    make \
        X11INC=/usr/include/X11 \
        X11LIB=/usr/lib/X11
}

package() {
    cd $_pkgname
    make PREFIX=/usr DESTDIR="$pkgdir" install
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
