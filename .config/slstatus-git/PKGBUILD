# Maintainer: Tin Thai <thaibachthanhtin@gmail.com>

_pkgname='slstatus'
pkgname="${_pkgname}-git"
pkgver=r604.f68f492
pkgrel=1
pkgdesc='A status monitor for window managers'
arch=('i686' 'x86_64')
url='https://tools.suckless.org/slstatus'
license=('custom:ISC')
depends=('libx11')
makedepends=('git')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
_patches=(
  'slstatus-rm-netspeeds-trailing-space-20201104-b14e039.diff'
)
sha256sums=(
  'SKIP'
  'SKIP'
  'fabbb1a14e86f3309cf03f45f6043b525ca1c49cf383e7fbc36420f19260e797'
)
source=(
  "git+https://git.suckless.org/${_pkgname}"
  'config.h'
  ${_patches[@]}
)

pkgver() {
  cd "${_pkgname}"
  (
  set -o pipefail
  /usr/bin/git describe --long 2>/dev/null |
    /usr/bin/sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    /usr/bin/printf "r%s.%s" "$(/usr/bin/git rev-list --count HEAD)"\
      "$(/usr/bin/git rev-parse --short HEAD)"
  )
}

prepare() {
  cd "${_pkgname}"
  # Copy sym linked config.h in src/ to src/slstatus/ dir.
  if [ -f "${srcdir}/config.h" ]
  then
    /usr/bin/cp --force --verbose "${srcdir}/config.h" config.h
  fi
  # Patch patches.
  local patch_file
  for patch_file in ${_patches[@]##*/}
  do
    /usr/bin/echo "Applying ${patch_file}..."
    /usr/bin/patch --silent --input="${srcdir}/${patch_file}"
  done
  # Do not overwrite system flags in /etc/makepkg.conf file.
  /usr/bin/sed --in-place 's/^\(\(C\|LD\)FLAGS *\)=/\1+=/g' config.mk
}

build() {
  cd "${_pkgname}"
  # Don't build unused components.
  # TODO: How to split long sed expression into multi lines?
  local unused_comps="(battery|cat|entropy|hostname|ip|kernel_release|keyboard_indicators|keymap|num_files|run_command|swap|uptime|user|volume)"
  /usr/bin/sed -E -i "/^[[:space:]]*components\/${unused_comps}/d" Makefile
  /usr/bin/make\
    X11INC=/usr/include/X11\
    X11LIB=/usr/lib/X11
}

package() {
  cd "${_pkgname}"
  /usr/bin/make DESTDIR="${pkgdir}" PREFIX='/usr' install
  local ins_opts='-D --mode=644'
  /usr/bin/install ${ins_opts}\
    LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  /usr/bin/install ${ins_opts}\
    README  "${pkgdir}/usr/share/doc/${pkgname}/README"
}

