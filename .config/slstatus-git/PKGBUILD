# Maintainer: Tin Thai <thaibachthanhtin@gmail.com>

_pkgname=slstatus
pkgname=${_pkgname}-git
pkgver=r552.b14e039
pkgrel=1
pkgdesc='A status monitor for window managers.'
arch=('i686' 'x86_64')
url='https://tools.suckless.org/slstatus'
depends=('libx11')
makedepends=('git')
license=('custom:ISC')
_patches=(
    'slstatus-rmtrailingspacenetspeeds-20201104-b14e039.diff'
)
source=(
    "git+https://git.suckless.org/${_pkgname}"
    'config.h'
    ${_patches[@]}
)
sha256sums=(
    'SKIP'
    '2abcf16d56b26a4da0e30e89054ffd21665066044deabc4e88c3c498c3e96e75'
    '5d27345c355d936141b3cc6bea06010c5c3bb7accda11cd835e785891fe8ae05'
)

pkgver() {
    cd $_pkgname
    (
        set -o pipefail
        git describe --long 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
        printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
    )
}

prepare() {
    cd $_pkgname
    if [ -f "${srcdir}/config.h" ]; then
        echo "Copying config.h ..."
        cp -f "${srcdir}/config.h" .
    fi
    for patch in ${_patches[@]}; do
        pfile=${patch##*/}
        echo "Applying ${pfile} ..."
        git apply "${srcdir}/${pfile}"
    done
}

build() {
    cd $_pkgname
    make \
        X11INC=/usr/include/X11 \
        X11LIB=/usr/lib/X11
}

package() {
    cd $_pkgname
    make PREFIX=/usr DESTDIR="$pkgdir" install
    install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
